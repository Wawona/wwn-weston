{ lib, pkgs, buildPackages, common, buildModule, simulator ? false, iosToolchain ? null, xcodeUtils ? iosToolchain }:

let
  westonSimpleShmSrc = pkgs.callPackage ./patched-src.nix { };
  libwayland = buildModule.buildForVisionOS "libwayland" { inherit simulator; };
  epollShim  = buildModule.buildForVisionOS "epoll-shim" { inherit simulator; };
  sdkName = if simulator then "XRSimulator" else "XROS";
  deploymentTarget = "26.0";
  targetFlag =
    if simulator
    then "-target arm64-apple-xros${deploymentTarget}-simulator"
    else "-target arm64-apple-xros${deploymentTarget}";
in
pkgs.stdenv.mkDerivation {
  name = "libweston-simple-shm-visionos";
  src = westonSimpleShmSrc;
  __noChroot = true;
  nativeBuildInputs = [ xcodeUtils.findXcodeScript ];

  buildPhase = ''
    if [ -z "''${XCODE_APP:-}" ]; then
      XCODE_APP=$(${xcodeUtils.findXcodeScript}/bin/find-xcode || true)
      if [ -n "$XCODE_APP" ]; then
        export DEVELOPER_DIR="$XCODE_APP/Contents/Developer"
        export SDKROOT="$DEVELOPER_DIR/Platforms/${sdkName}.platform/Developer/SDKs/${sdkName}.sdk"
      fi
    fi

    IOS_CC="$DEVELOPER_DIR/Toolchains/XcodeDefault.xctoolchain/usr/bin/clang"
    OBJ_FILES=""
    for src_file in clients/simple-shm.c xdg-shell-protocol.c fullscreen-shell-unstable-v1-protocol.c; do
      obj_file="$(basename $src_file .c).o"
      $IOS_CC -c "$src_file" \
        -I. -Ishared -Iinclude \
        -I${libwayland}/include/wayland -I${libwayland}/include \
        -I${epollShim}/include/libepoll-shim \
        -fPIC -arch arm64 ${targetFlag} -isysroot "$SDKROOT" \
        -o "$obj_file"
      OBJ_FILES="$OBJ_FILES $obj_file"
    done

    ar rcs libweston_simple_shm.a $OBJ_FILES
  '';

  installPhase = ''
    mkdir -p $out/lib
    cp libweston_simple_shm.a $out/lib/
  '';
}
