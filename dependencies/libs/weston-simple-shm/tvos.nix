{
  lib,
  pkgs,
  buildPackages,
  common,
  buildModule,
  simulator ? false,
  iosToolchain ? null,
  xcodeUtils ? iosToolchain,
}:

let
  westonSimpleShmSrc = pkgs.callPackage ./patched-src.nix { };
  libwayland = buildModule.buildForTVOS "libwayland" { inherit simulator; };
  epollShim = buildModule.buildForTVOS "epoll-shim" { inherit simulator; };
  sdkPlatform = if simulator then "AppleTVSimulator" else "AppleTVOS";
  minVerFlag =
    if simulator then
      "-mtvos-simulator-version-min=${iosToolchain.deploymentTarget}"
    else
      "-mtvos-version-min=${iosToolchain.deploymentTarget}";
in
pkgs.stdenv.mkDerivation {
  name = "libweston-simple-shm-tvos";
  src = westonSimpleShmSrc;
  __noChroot = true;

  nativeBuildInputs = [ xcodeUtils.findXcodeScript ];

  buildPhase = ''
    if [ -z "''${XCODE_APP:-}" ]; then
      XCODE_APP=$(${xcodeUtils.findXcodeScript}/bin/find-xcode || true)
      if [ -n "$XCODE_APP" ]; then
        export DEVELOPER_DIR="$XCODE_APP/Contents/Developer"
        export SDKROOT="$DEVELOPER_DIR/Platforms/${sdkPlatform}.platform/Developer/SDKs/${sdkPlatform}.sdk"
      fi
    fi

    IOS_CC="$DEVELOPER_DIR/Toolchains/XcodeDefault.xctoolchain/usr/bin/clang"
    IOS_ARCH="arm64"

    OBJ_FILES=""
    for src_file in clients/simple-shm.c xdg-shell-protocol.c fullscreen-shell-unstable-v1-protocol.c; do
      obj_file="$(basename $src_file .c).o"
      $IOS_CC -c "$src_file" \
         -I. \
         -Ishared \
         -Iinclude \
         -I${libwayland}/include/wayland \
         -I${libwayland}/include \
         -I${epollShim}/include/libepoll-shim \
         -fPIC -arch $IOS_ARCH -isysroot "$SDKROOT" ${minVerFlag} \
         -o "$obj_file"
      OBJ_FILES="$OBJ_FILES $obj_file"
    done

    ar rcs libweston_simple_shm.a $OBJ_FILES
  '';

  installPhase = ''
    mkdir -p $out/lib
    cp libweston_simple_shm.a $out/lib/
  '';
}
